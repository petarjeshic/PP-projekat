
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import java.io.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.util.MyTab;
import rs.etf.pp1.symboltable.concepts.*;
import rs.etf.pp1.symboltable.structure.SymbolDataStructure;
import java.util.Stack;
import java.util.Collection;
import java.util.Iterator;
import java.util.HashMap;
import java.util.Map;
import rs.etf.pp1.mj.runtime.Code;
parser code {:
	
	boolean errorDetected=false;
	Logger log=Logger.getLogger(getClass());
	public void report_fatal_error(String message,Object info)throws java.lang.Exception{
		done_parsing();
		report_error(message,info);
	}
	public void syntax_error(Symbol cur_token){
		report_error("\nSintaksna greska",cur_token);
	}
	public void unrecovered_syntax_error(Symbol cur_token)throws java.lang.Exception{
		report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti",cur_token);
	}
	public void report_error(String message,Object info){
		errorDetected=true;
		StringBuilder msg=new StringBuilder(message);
		if(info instanceof Symbol)
			msg.append(" na liniji ").append(((Symbol)info).left);
		log.error(msg.toString());
	}
	public void report_info(String message,Object info){
		StringBuilder msg=new StringBuilder(message);
		if(info instanceof Symbol)
			msg.append(" na liniji ").append(((Symbol)info).left);
		log.info(msg.toString());
	}
:}

action code {:
	Obj currentMethod = null;
	Obj _this;
	Obj currentClass;
	
	Struct currentType, currentMethType;
	Struct varType = null;
	Struct newClass=null;
	
	boolean main=false;
	boolean returnFound=false;
	boolean in_class = false; 
	boolean in_method = false;
	boolean in_for=false;
	boolean left=false;
	boolean ok=true;
	
	int printCallCount;
	int array_count = 0;	
	int var_count = 0;
	int con_count = 0;
	int const_value=0;
	int mainDeclarations = 0;
	int formParsNum=0;
	
	
	Map<String, Obj> redefiningMap = new HashMap<String, Obj>(); 
	SymbolDataStructure extendsSymbols = null;
	
	Scope globalScope = null;
	Scope functionScope=null;
	Scope classScope=null;
	int global_scope=0;
	
	Stack<Obj> callingStack = new Stack<Obj>();
	Stack<Integer> actParsStack = new Stack<Integer>();
			
:}

init with {:
	MyTab.init(); 
:}


scan with{:
	Symbol s=this.getScanner().next_token();
	if(s!=null && s.value!=null)
		log.info(s.toString()+" "+s.value.toString());
	return s;
:}




terminal PROG,VOID,LBRACE,RBRACE,SEMI, LPAREN, RPAREN, COMMA,PRINT,RETURN,PLUS, LMEDBRACE,RMEDBRACE,DOT;
terminal EQUAL,GREATER,LOWER,BREAK,CLASS,ELSE,CONST,IF,NEW,READ,FOR,EXTENDS,CONTINUE,STATIC;
terminal MINUS,MULI,DIVI,MODI,EQEQ,NOTEQ,GREATEQ,LOWEQ,AND,OR,PLUSEQ,MINUSEQ,MULEQ,DIVEQ,MODEQ,PLUSPLUS,MINUSMINUS;
terminal DOLAR,DVOTACKA,TARABA;



terminal Integer NUMBER;
terminal Character CHAR;
terminal Boolean BOOL;
terminal String IDENT;

non terminal Program,MethodDecl,VarDeclList,MethodDeclList,VarDecl,StatementList;
non terminal Statement,ActPars,ActualPars,ClassName,Extends,OptionalMethods;
non terminal ConstDecl,Const,IdentList,Var,VarList;
non terminal ClassDecl,DesignatorStatement,Condition,DesignatorStatementList;
non terminal ConditionList,CondTermList,ClassVarDeclList;
non terminal CondFact,If,ClassVarDecl,FormParsList2,FormParsList;
non terminal AddopLeft,AddopRight,MulopRight,MulopLeft,CondTerm;
non terminal CondFactList,Matched,Unmatched,SomeDeclList;

non terminal Obj ProgName,Factor,Expr,Term,Designator,FieldDeclList,Field;
non terminal Struct Type;
non terminal Integer Assignop,NumConstList,Relop,Addop,Mulop,FormPars;

Program ::= PROG ProgName:p 
			{:
				MyTab.openScope();
				
			:} 
			SomeDeclList LBRACE MethodDeclList RBRACE
			{:
				Code.dataSize=MyTab.currentScope().getnVars();	
				MyTab.chainLocalSymbols(p);
				MyTab.closeScope();
				
				
				
				parser.report_info("Broj globalnih prostih promenjivih:" +var_count,null);
				parser.report_info("Broj globalnih nizova:" + array_count,null);
				parser.report_info("Broj globalnih konstanti:" + con_count,null);
				parser.report_info("Broj deklaracija promenljivih u main-u:" + mainDeclarations,null);	
			:}
			;
ProgName ::= IDENT:pName
			{:
			
			globalScope=MyTab.currentScope();
			
			Obj len = MyTab.find("len");
			len.setAdr(Code.pc);
			Code.put(Code.enter);
			Code.put(1);
			Code.put(1);
			Code.put(Code.load);
			Code.put(0);
			Code.put(Code.arraylength);
			Code.put(Code.exit);
			Code.put(Code.return_);
			 
			Obj chr = MyTab.find("chr");
			chr.setAdr(Code.pc);
			Code.put(Code.enter);
			Code.put(1);
			Code.put(1);
			Code.put(Code.load);
			Code.put(0);
			Code.put(Code.exit);
			Code.put(Code.return_);
			  
			Obj ord = MyTab.find("ord");
			ord.setAdr(Code.pc);
			Code.put(Code.enter);
			Code.put(1);
			Code.put(1);
			Code.put(Code.load);
			Code.put(0);
			Code.put(Code.exit);
			Code.put(Code.return_);
			RESULT= MyTab.insert(Obj.Prog,pName,MyTab.noType);
			 :};
   
SomeDeclList ::= SomeDeclList ConstDecl
				|
				SomeDeclList VarDecl
				|
				SomeDeclList ClassDecl
				|
				/*epsilon*/
				;
ConstDecl ::= CONST Type IDENT:name EQUAL IdentList:o SEMI
			 {:
				  	if(MyTab.currentScope.findSymbol(name) != null)
				  		parser.report_error("Greska na liniji: "+nameleft+". Vec postoji identifikator sa imenom: " + name,null);
				  	else {
				  		Obj temp = MyTab.insert(Obj.Con, name, currentType);
				  		temp.setAdr(const_value);
				  		if(!o.equals(currentType))
				  			parser.report_error("Greska na liniji: " + nameleft+" . Tip koji se dodeljuje ne odgovara tipu kome se dodeljuje. ",null);
				  		parser.report_info("Deklarisana globalna konstanta " + name + " na liniji " + nameleft, null);
				  	}
				  	if(global_scope==0)
					con_count++;
			  :}
			  |
			  CONST error SEMI:s 
			  {: 
			  	parser.report_info("Uspesan oporavak od greske na liniji " + sleft + " pri definisanju konstante.",null); 
		  	  :};
IdentList ::=IdentList COMMA IDENT:name EQUAL Const:o 
			{:
				if(MyTab.currentScope.findSymbol(name) != null)
				  		parser.report_error("Greska na liniji: "+nameleft+". Vec postoji identifikator sa imenom: " + name,null);
				  	else {
				  		Obj temp = MyTab.insert(Obj.Con, name, currentType);
				  		temp.setAdr(const_value);
				  		if(!o.equals(currentType))
				  			parser.report_error("Greska na liniji: " + nameleft+" . Tip koji se dodeljuje ne odgovara tipu kome se dodeljuje. ",null);
				  		parser.report_info("Deklarisana globalna konstanta " +name + " na liniji " + nameleft, null);
				  	}
				  if(global_scope==0)
				  con_count++;
				  RESULT=o;
			:}
			|
			Const:o
			{:
				RESULT=o;
			:}
			;
Const ::= NUMBER:num
			{:
				const_value=num;
				RESULT = MyTab.intType;  
				
			:}
			|CHAR:chr
			{:
				const_value=(int)chr;
				RESULT = MyTab.charType; 
			:}
			|BOOL:bool
			{:
				if(bool==true)
				const_value = 1;
				else 
				const_value = 0;
				RESULT = MyTab.boolType; 
			:}
			;
VarDecl ::= Type VarList SEMI
			{:
				currentType=null;
			:}
				|
		    Type error:e
		   	{: 
		   		currentType=null;
		  		parser.report_info("Uspesan oporavak od greske pri definisanju globalne promenljive na liniji " + eleft,null); 
	  	   	:};
VarList ::= Var COMMA VarList
			{:
			if(main)
			mainDeclarations++;
			:}
			| 
			Var
			{:
			if(main)
			mainDeclarations++;
			:}
			| 
			error COMMA:c VarList
			{:
			parser.report_info("Uspesan oporavak od greske pri definisanju globalne promenljive na liniji " + cleft,null); 
			:}
			;
Var ::= IDENT:name
		{:	
			Obj var = MyTab.find(name);
			if(var != MyTab.noObj) {
				if(global_scope==0)
					parser.report_error("Semanticka greska na liniji " + nameleft + " : Ime promenljive vec postoji!", null);
				else if(currentType != null) 
					MyTab.insert(Obj.Var, name, currentType);
					
			}
			else if(currentType != null) {
				MyTab.insert(Obj.Var, name, currentType);
				parser.report_info("Deklarisana promenljiva " + name + " na liniji " + nameleft, null);
			}
			if(global_scope==0)
			var_count++;
		:}
 			|
 		IDENT:name LMEDBRACE RMEDBRACE
 		{: 
			Obj var = MyTab.find(name);
			if(var != MyTab.noObj) {
				if(global_scope==0)
					parser.report_error("Semanticka greska na liniji " + nameleft + " : Ime promenljive vec postoji!", null);
				else if(currentType != null) 
					MyTab.insert(Obj.Var, name, currentType);
			}
			else if(currentType != null) {
				Struct arrayType = new Struct(Struct.Array);
				arrayType.setElementType(currentType);
				MyTab.insert(Obj.Var, name, arrayType);
				parser.report_info("Deklarisan niz " + name + " na liniji " + nameleft, null);
			}
			if(global_scope==0)
			array_count++;
		:};
ClassDecl ::= CLASS ClassName:c Extends 	
			  {:		  	
			  	if(extendsSymbols!=null) {
				  	for(Obj symbol: extendsSymbols.symbols()) {
				  			if(symbol.getKind() == Obj.Fld) {	
				  				if(MyTab.currentScope().findSymbol(symbol.getName()) == null) {
				  					Obj obj = MyTab.insert(symbol.getKind(), symbol.getName(), symbol.getType());
				  				}
				  			}
				  			else if(symbol.getKind() == Obj.Meth) {
				  				if(MyTab.currentScope().findSymbol(symbol.getName()) == null) {
				
									Obj currentMeth = MyTab.insert(symbol.getKind(), symbol.getName(), symbol.getType());
										
									currentMeth.setLevel(symbol.getLevel());
									currentMeth.setAdr(symbol.getAdr());
										
									MyTab.openScope();
									newClass = new Struct(Struct.Class);
									Obj obj = MyTab.insert(Obj.Var, "this", newClass);
									global_scope++;	
									Collection<Obj> locals = symbol.getLocalSymbols();
									Iterator it = locals.iterator();
										
									for(int i=0; i<symbol.getLevel(); i++) {
										if(it.hasNext()) {
											MyTab.currentScope().addToLocals((Obj)it.next());
										}
									}
									MyTab.chainLocalSymbols(currentMeth);
									global_scope--;
									MyTab.closeScope();
								}
				  			}
				  	}
					
					MyTab.chainLocalSymbols(newClass);
				}
				
			  :}
			  LBRACE ClassVarDeclList OptionalMethods RBRACE 
			  {: 
					
				MyTab.chainLocalSymbols(newClass);
				
			  	if(true) {									  
			  		Obj obj = new Obj(Obj.Var, "#vt_finish" + globalScope.getnVars(), MyTab.intType);
			  		globalScope.addToLocals(obj);
			  		obj.setLevel(0);
			  		global_scope--;
			  		MyTab.closeScope();
			  		parser.report_info("Definisana klasa " + c + " na liniji " + cleft, null);
			  	}
			  	
			  	classScope = null;
			  	newClass = null;
			  	extendsSymbols = null;
			  	
			  	redefiningMap.clear();
			  :} 
			  ;
OptionalMethods::=LBRACE MethodDeclList RBRACE
				|
				/*epsilon*/;			  
ClassName ::= IDENT:className 
			  {:
			  	Obj classNode = MyTab.find(className);
			  	if(classNode != MyTab.noObj) {
			  		parser.report_error("Semanticka greska na liniji " + classNameleft + " : Ime klase se vec koristi! ", null);
			  		RESULT = MyTab.noObj;
			  		ok = false;
			  	}
			  	else {				
			  		newClass = new Struct(Struct.Class);
				  	
				  	Obj obj = MyTab.insert(Obj.Type, className, newClass);				  	
				  	
				  	RESULT=className;
				  	MyTab.openScope();
				  	global_scope++;
				  	classScope = MyTab.currentScope();
				  	
				  	Obj vtPtr = MyTab.insert(Obj.Fld, "vtPtr", MyTab.intType);
				  					  	
				  	MyTab.chainLocalSymbols(newClass);
				  	
				  	parser.report_info("Obradjuje se klasa " + className + " na liniji " + classNameleft, null);
				  	ok = true;
					
				}
			  :};

Extends ::= EXTENDS Type:t 
			{:
				if(t.getKind() != Struct.Class) {
					parser.report_error("Semanticka greska na liniji " + tleft + ": Tip za extends mora biti klasa! ", null);
				}
				else {
					
					newClass.setElementType(t);	
					
					extendsSymbols = t.getMembersTable();
					
					
					MyTab.chainLocalSymbols(newClass);
					RESULT=t;
				}
			:}   
			|
			EXTENDS error
			| 
			/*epsilon*/;	

ClassVarDeclList ::= ClassVarDeclList ClassVarDecl | /*epsilon*/;

ClassVarDecl ::= Type FieldDeclList SEMI
				 |
				 Type error:e SEMI
				 {: 
				 parser.report_error("Uspesan oporavak od greske na liniji: "+eleft+".",null); :} 
				 |
				 error
				 |
				 Type error
				 ;

FieldDeclList ::= FieldDeclList COMMA Field | Field ;

Field ::=IDENT:vName 
		  {: 

			Obj var = MyTab.currentScope().findSymbol(vName);
			
			if(var != null) {
			
				if(newClass.getElemType() !=null && newClass.getElemType().getMembersTable().searchKey(vName) == null)
					
					parser.report_error("Semanticka greska na liniji " + vNameleft + " : Ime promenljive vec postoji!", null);
				
				else {
					MyTab.currentScope().getLocals().deleteKey(vName);
					
					MyTab.insert(Obj.Fld, vName, currentType);
				
					MyTab.chainLocalSymbols(newClass);
					
					parser.report_info("Deklarisano polje " + vName + " tipa " + currentType + " na liniji " + vNameleft, null);
				}
			}
			else if(currentType != null) {
				MyTab.insert(Obj.Fld, vName, currentType);
				
				MyTab.chainLocalSymbols(newClass);
				
				parser.report_info("Deklarisano polje " + vName + " tipa " + currentType + " na liniji " + vNameleft, null);
			}
		  :}
		  |
		  IDENT:vName LMEDBRACE RMEDBRACE 
		  {: 
			
			Obj var = MyTab.currentScope().findSymbol(vName);
			if(var != null) {
				if(newClass.getElemType() !=null && newClass.getElemType().getMembersTable().searchKey(vName) == null)
					
					parser.report_error("Semanticka greska na liniji " + vNameleft + " : Ime promenljive vec postoji!", null);
				
				else {
					MyTab.currentScope().getLocals().deleteKey(vName);
					
					Struct arrayType = new Struct(Struct.Array);
					arrayType.setElementType(currentType);
					MyTab.insert(Obj.Fld, vName, arrayType);
					
					MyTab.chainLocalSymbols(newClass);
					
					parser.report_info("Deklarisan niz kao polje " + vName + " na liniji " + vNameleft, null);
				}
			}
			else if(currentType != null) {
				Struct arrayType = new Struct(Struct.Array);
				arrayType.setElementType(currentType);
				MyTab.insert(Obj.Fld, vName, arrayType);
				
				MyTab.chainLocalSymbols(newClass);
				
				parser.report_info("Deklarisan niz kao polje " + vName + " na liniji " + vNameleft, null);
			}
		  :}
		  ; 
MethodDeclList ::= MethodDeclList:m MethodDecl
				  {:
				   RESULT=m;
				  :}
				  |
				  /*epsilon*/
				  ;
MethodDecl ::=Type:retType IDENT:methName 
				{:
					Obj funkcija=MyTab.find(methName);
					global_scope++;
					if(funkcija==MyTab.noObj){
						parser.report_info("Obradjuje se funkcija: "+methName+" na liniji "+methNameleft,null);
						currentMethod=MyTab.insert(Obj.Meth,methName,retType);
						if(methName.equalsIgnoreCase("main")){
							Code.mainPc=Code.pc;
							main=true;
							}
						currentMethType=currentType;
						MyTab.openScope();
						functionScope=MyTab.currentScope();
						in_method=true;
						}
					else {
						parser.report_error("Semanticka greska na liniji " + methNameleft + " : Ime funkcije vec postoji!", null);
					}
					
				:}
				LPAREN FormParsList2  RPAREN VarDeclList LBRACE
				{:
				currentMethod.setLevel(formParsNum);
				Code.put(Code.enter);
				Code.put(formParsNum);
				Code.put(functionScope.getnVars()); 
			:} 
			 StatementList RBRACE
			{: 
				if(in_class) 
					  		_this = MyTab.insert(Obj.Var, "this", MyTab.noType);
				if(!returnFound && retType != MyTab.noType)
					parser.report_error("Semanticka greska na liniji: "+methNameleft+". Funkcija: "+methName+" nema return iskaz.",null);
				MyTab.chainLocalSymbols(currentMethod);
				currentMethod.setLocals(MyTab.currentScope.getLocals());
				MyTab.closeScope();
				Code.put(Code.exit);
				Code.put(Code.return_);
				if(currentMethType!=MyTab.noType && returnFound==false){
					Code.put(Code.trap);
					Code.put(1);
				}
				global_scope--;
				functionScope=null;
				main=false;
				returnFound=false;
				in_method=false;
				currentMethType=null;
				formParsNum=0;
				currentMethod=null;	
			:}
			|
			STATIC Type:retType IDENT:methName 
			{:
			global_scope++;
			Obj funkcija=MyTab.find(methName);
					if(funkcija==MyTab.noObj){
						parser.report_info("Obradjuje se staticka funkcija: "+methName+" na liniji "+methNameleft,null);
						currentMethod=MyTab.insert(Obj.Meth,methName,retType);
						currentMethType=currentType;
						MyTab.openScope();
						functionScope=MyTab.currentScope();
						in_method=true;
						}
					else {
						parser.report_error("Semanticka greska na liniji " + methNameleft + " : Ime funkcije vec postoji!", null);
					}
			
			:}
			LPAREN FormParsList2 RPAREN VarDeclList LBRACE
			{:  
				if(!in_class==true)
					//parser.report_error("Greska na liniji: "+retTypeleft+". Staticka funkcija: "+methName+" mora biti unutar klase.",null);
				currentMethod.setLevel(formParsNum);
				Code.put(Code.enter);
				Code.put(formParsNum);
				Code.put(functionScope.getnVars()); 
			:} 
			 StatementList RBRACE
			{: 
				if(in_class) 
					  		_this = MyTab.insert(Obj.Var, "this", MyTab.noType);
				if(!returnFound && retType != MyTab.noType)
					parser.report_error("Semanticka greska na liniji: "+methNameleft+". Funkcija: "+methName+" nema return iskaz.",null);
				MyTab.chainLocalSymbols(currentMethod);
				currentMethod.setLocals(MyTab.currentScope.getLocals());
				MyTab.closeScope();
				Code.put(Code.exit);
				Code.put(Code.return_);
				if(currentMethType!=MyTab.noType && returnFound==false){
					Code.put(Code.trap);
					Code.put(1);
				}
				global_scope--;
				functionScope=null;
				returnFound=false;
				in_method=false;
				currentMethType=null;
				formParsNum=0;
				currentMethod=null;	
			:}
			;			
			
VarDeclList ::= VarDeclList VarDecl
				|
				/*epsilon*/
				;
FormParsList2 ::= FormParsList:f 
				  {:
				  	if(main) {
				  		parser.report_error("Semanticka greska na liniji " + fleft + ": main metoda ne sme da ima parametre!", null);
				  	}
				  :}
				  |
				  /*epsilon*/ 
				  ;

FormParsList ::= FormPars COMMA FormParsList
				 {:
				 	formParsNum++;
				 :}
				 |
				 FormPars
				 {:
				 	formParsNum++;
				 :};
FormPars ::= Type:type IDENT:name LMEDBRACE RMEDBRACE
			{:
				parser.report_info("Parametar funkcije: "+name+" na liniji "+nameleft,null);
				if(MyTab.currentScope.findSymbol(name)!=null)
					{
						parser.report_error("Vec postoji promenljiva sa imenom: " + name,null);
					}
				else {
					Struct arrayType=new Struct(Struct.Array);
					arrayType.setElementType(currentType);
					Obj temp = MyTab.insert(Obj.Var, name, arrayType);
					}
			
			:}
			|
			Type:type IDENT:name 
			{:
				parser.report_info("Parametar funkcije: "+name+" na liniji "+nameleft,null);
				if(MyTab.currentScope.findSymbol(name)!=null)
					{
						parser.report_error("Vec postoji promenljiva sa imenom: " + name,null);
					}
				else {
					Obj temp = MyTab.insert(Obj.Var, name, currentType);
					}
			
			:}
			|
			Type:typeName DOT DOT DOT IDENT
			{::}
			;

Type ::= VOID
		{:
			RESULT=MyTab.noType;
		:}
		|
		IDENT:vType
		{:
			Obj typeNode = MyTab.find(vType);
			if(typeNode != MyTab.noObj) {
				if(typeNode.getKind() == Obj.Type) {
					currentType = typeNode.getType();
					
					if(currentType.getKind() == Struct.Class)
						parser.report_info("Koriscenje klase " + vType + " kao tipa na liniji " + vTypeleft, null);
					
					RESULT = currentType;
				}
				else {
					parser.report_error("Ime " + vType + " nije tip!" , null);
					RESULT = MyTab.noType;
				}
			}
			else {
				parser.report_error("Semanticka greska na liniji " + vTypeleft + ":" + " Nije pronadjen tip " + vType + "!" , null);
				RESULT = MyTab.noType;
			}
		:}
		;
		
Statement ::= Matched | Unmatched;
If::=IF LPAREN Condition:cond
			{:
				if(cond.equals(Struct.Bool)){
					parser.report_error("Greska na liniji: "+condleft+". Uslov mora biti boolean tipa.",null);
				}
			:} 
			RPAREN;
Unmatched ::= If Statement
			| 
			If Matched ELSE Unmatched;
Matched ::= DesignatorStatement SEMI
			|
			Designator:func
			{:
				callingStack.push(func); 
				actParsStack.push(0);
			:}
			 LPAREN ActualPars RPAREN SEMI
			{:
			
			if(Obj.Meth == func.getKind()){
				parser.report_info("Pronadjen poziv funkcije "+func.getName()+" na liniji: "+funcleft,null);
				int formalPars = func.getLevel();
				if(formalPars > actParsStack.peek()) {	
					parser.report_error("Semanticka greska na liniji " + funcleft + ": broj stvarnih i formalnih parametara mora biti isti!", null);
				}
				else {	
				
							int destAdr = func.getAdr() - Code.pc;	//adresa prve instrukcije u funkciji minus pc.
							Code.put(Code.call);
							Code.put2(destAdr);	//put2 stavlja relativan skok.
							if(func.getType()!=null) {
								Code.put(Code.pop);
							}
							
				}
				}
			else {
				parser.report_error("Greska na liniji: "+funcleft+" ime: "+func.getName()+" nije funkcija.",null);
				RESULT=MyTab.noType;
				}
			callingStack.pop();
			actParsStack.pop();
			:}
			|
			If Matched ELSE Matched
			|
			FOR LPAREN DesignatorStatementList SEMI ConditionList SEMI DesignatorStatementList RPAREN
			{:
				in_for=true;
			:}
			Matched
			{:
				in_for=false;
			:}
			|
			BREAK:b SEMI
			{:
				if(in_for==false)
					parser.report_error("Greska na liniji: "+bleft+". Break mora biti unutar for petlje.",null);
			:}
			|
			CONTINUE:c SEMI
			{:
				if(in_for==false)
					parser.report_error("Greska na liniji: "+cleft+". Continue mora biti unutar for petlje.",null);
			:}
			|
			RETURN Expr:t
			{:	
				returnFound=true;
				if(in_method=false){
					parser.report_error("Greska na liniji: "+tleft+". Iskaz RETURN se moze koristiti samo unutar metoda i funkcija.",null);
				}
				if(currentMethType==MyTab.noType){
					parser.report_error("Greska na liniji: "+tleft+". Povratni tip funkcije je VOID i ne moze imati vrednost u return iskazu.",null);
				}
				if(currentMethType.getKind()!=t.getType().getKind()){
					parser.report_error("Greska na liniji: "+tleft+". Tip izraza u return naredbi se ne slaze sa tipom povratne vrednosti funkcije.",null);
				
				}
				Code.put(Code.exit);
				Code.put(Code.return_);
				
			:} 
			SEMI
			|
			RETURN:linija SEMI
			{:
				if (currentMethType != MyTab.noType)
					parser.report_error("Greska na liniji "+ linijaleft + ":metod ne sme imati return bez izraza jer nije void!",null);
			:}
			|
			READ LPAREN Designator:des RPAREN SEMI
			{:
				if (des.getKind()!=Struct.Int&&des.getKind()!=Struct.Char&&des.getKind()!=Struct.Bool)
					parser.report_error("Greska na liniji "+ desleft + ". Izraz mora biti integer, boolean ili char.",null);
				else{	
					if(des.getKind() == Struct.Int) {
								Code.put(Code.read);	
								Code.store(des); 			
					}
					else if(des.getKind() == Struct.Char) {
								Code.put(Code.bread);
								Code.store(des);
					}
					else if(des.getKind() == Struct.Bool) {
								Code.put(Code.read);
								Code.store(des);
					}
				}
			:}
			|
			PRINT LPAREN Expr:e NumConstList:numC RPAREN SEMI
			{:
				if (e.getType().getKind()!=Struct.Int&&e.getType().getKind()!=Struct.Char&&e.getType().getKind()!=Struct.Bool)
					parser.report_error("Greska na liniji "+ eleft + ". Izraz mora biti integer, boolean ili char.",null);
				if(e.getType().getKind()==Struct.Int){
					Code.loadConst(numC.intValue());
					Code.put(Code.print);
				}
				if(e.getType().getKind()==Struct.Char){
					Code.loadConst(numC.intValue());
					Code.put(Code.bprint);
				}
				if(e.getType().getKind()==Struct.Bool){
					Code.loadConst(numC.intValue());
					Code.put(Code.print);
				}
			:}
			|
			LBRACE StatementList RBRACE
			 ;
StatementList ::= StatementList Statement
			     |
			     /*epsilon*/
			     ;
ConditionList ::=Condition| /*epsilon*/;
NumConstList ::= COMMA NUMBER:n {:RESULT=n;:}|/*epsilon*/{:RESULT=1;:};
DesignatorStatementList ::=DesignatorStatement
							|
							/*epsilon*/
							;
DesignatorStatement ::= Designator:dest Assignop:oper Expr:e
						{:
							Obj ex=MyTab.find(e.toString());
							if(dest.getType().getKind()!=Struct.Array&&dest.getType().getKind()!=Struct.Int&&dest.getType().getKind()!=Struct.Char&&dest.getType().getKind()!=Struct.Bool&&dest.getType().getKind()!=Struct.Class){
								parser.report_error( "Tip kome se dodeljuje mora biti promenljiva, element niza ili polje unutar objekta. Greska na liniji " + destleft,null);
		 						RESULT=MyTab.noType;
							}
							else
							if (e.getType().getKind()!=dest.getType().getKind()) {
		 						parser.report_error( "Ovaj tip se ne moze dodeliti promenjivoj tipa: " + dest.getType().getKind() + " - Greska na liniji " + destleft,null);
		 						RESULT=MyTab.noType;
		 						}
		 					if(oper.intValue()!=Code.eq){
		 					Code.load(dest);
		 					Code.put(oper.intValue());
		 					if(oper.intValue()==Code.sub)
		 					Code.put(Code.neg);
		 					}
		 					Code.store(dest);
							RESULT=dest;
						:}
						|
						Designator:dest PLUSPLUS:op
						{:
							if(dest.getType().getKind()!=Struct.Array&&dest.getType().getKind()!=Struct.Int&&dest.getType().getKind()!=Struct.Char&&dest.getType().getKind()!=Struct.Bool&&dest.getType().getKind()!=Struct.Class){
								parser.report_error( "Tip kome se dodeljuje mora biti promenljiva, element niza ili polje unutar objekta. Greska na liniji " + destleft,null);
		 						RESULT=MyTab.noType;
							}
							if(dest.getKind() == Obj.Elem) {
									Code.put(Code.dup2);
								}
								if(dest.getKind()==Struct.Int) {
									Code.load(dest);
									Code.loadConst(1);
									Code.put(Code.add); 
									Code.store(dest);
								}
								else
		 							parser.report_error("Promenljiva: "+dest+" mora biti tipa int. - Greska na liniji " + destleft,null);
								RESULT=dest;
						:}
						|
						Designator:dest MINUSMINUS:op
						{:
								if(dest.getKind() == Obj.Elem) {
									Code.put(Code.dup2);
								}
								if(dest.getKind()==Struct.Int) {
									Code.load(dest);
									Code.loadConst(1);
									Code.put(Code.sub); 
									Code.store(dest);
								}
								else
		 							parser.report_error("Promenljiva: "+dest+" mora biti tipa int. - Greska na liniji " + destleft,null);
								RESULT=dest;
						:}
						;
Condition ::= CondTerm:cond CondTermList
			 {:
			 	RESULT=0;
			 :}
			 ;
CondTermList ::= CondTermList:c OR CondTerm
				{:
					RESULT=c;
				:}
				|
				/*epsilon*/
				;
CondTerm ::= CondFact:cond CondFactList
			{:
				RESULT=0;
			:}
			;				
CondFactList ::= CondFactList:li AND CondFact
				{:
					RESULT=li;
				:}
				|
				/*epsilon*/
				;
CondFact ::= Expr:e1 Relop:op Expr:e2
			{:
			 	if(e1.getType().getKind()!=e2.getType().getKind()) {
		 			parser.report_error("Nekompatibilni tipovi na liniji " + e1left,null);
		 		}
		 		RESULT=op;
		 	:}
			|
			Expr:e
			{:
				if(e.getType().getKind() != Struct.Bool) {
			 		parser.report_error("Semanticka greska na liniji " + eleft + ": Izraz nije tipa bool! ", null);
			 	}
			 	else {
			 		Code.loadConst(1);
			 		
			 		RESULT = Code.eq;
			 	}
			:};
				
Expr ::=Term:e Addop:op Term:t
		{:
			if(e.getType().getKind()!=t.getType().getKind()){
				parser.report_error("Greska na liniji: "+tleft+" nekompatibilni tipovi u izrazu.",null);
				RESULT =MyTab.noObj;
			}
			if(e.getType().getKind()!=Struct.Int){
				parser.report_error("Greska na liniji: "+eleft+". Izraz pre operacije mora biti tipa int.",null);
				RESULT=MyTab.noObj;
			}
			if(t.getType().getKind()!=Struct.Int){
				parser.report_error("Greska na liniji: "+tleft+". Izraz mora biti tipa int.",null);
				RESULT=MyTab.noObj;
			}
			Code.put(op.intValue());
			RESULT=e;
		:}
		|
		MINUS Term:t
		{:
		if(t.getType().getKind()!=Struct.Int){
			parser.report_error("Greska na liniji:"+tleft+". Promenljiva u izrazu mora biti tipa Int.",null);
			RESULT=MyTab.noObj;
		}
		else{
			Code.put(Code.neg);
			RESULT=t;
		}
		:}
		|
		Term:t
		{:
		RESULT=t;
		:}
		;

Term ::= Factor:f1
		{:
		 RESULT=f1; 
		 :}
		 |
		 Term:t1 Mulop:op Factor:t2
		 {:
		 if(t1.getType().getKind()!=t2.getType().getKind()) {
			parser.report_error("Greska na liniji: "+t1left+" nekompatibilni tipovi u izrazu.",null);
			RESULT=MyTab.noObj;
		 }
		 if(t1.getType().getKind()!=Struct.Int||t2.getType().getKind()!=Struct.Int){
		 	parser.report_error("Greska na liniji: "+t1left+". Tip promenljive u izrazu mora biti Int.",null);
		 	RESULT=MyTab.noObj;
		 }
		 if(left==false){
		 	Obj term=MyTab.find(t1.getName());
		 	if(term==MyTab.noObj){
		 		parser.report_error("Greska na liniji: "+t1left+". Korisceni simbol ne postoji u tabeli simbola.",null);
		 		RESULT=MyTab.noObj;
		 	}
		 	if(term.getKind()!=Struct.Int&&term.getKind()!=Struct.Bool&&term.getKind()!=Struct.Char){
		 		parser.report_error("Greska na liniji: "+t1left+". Tip mora biti promenljiva, element niza ili polje unutar objekta.",null);
		 		RESULT=MyTab.noObj;
		 	}
		 }
		 if(op.intValue()!=1)
		 	Code.put(op.intValue());
		 else {
		 	Code.put(Code.dup);
		 	Code.put(Code.mul);
		 	Code.put(Code.neg);
		 	Code.put(Code.dup_x1);
		 	Code.put(Code.pop);
		 	Code.put(Code.dup);
		 	Code.put(Code.dup);
		 	Code.put(Code.mul);
		 	Code.put(Code.mul);
		 	Code.put(Code.add);
		 }	
		 left=false;
		 RESULT = t1;
		 :}
		 |
		 Term:t1 TARABA Factor:t2 DVOTACKA Factor:t3
		 {:
		 if(t1.getType().getKind()!=t2.getType().getKind()||t1.getType().getKind()!=t3.getType().getKind()) {
			parser.report_error("Greska na liniji: "+t1left+" nekompatibilni tipovi u izrazu.",null);
			RESULT=MyTab.noObj;
		 }
		 if(t1.getType().getKind()!=Struct.Int||t2.getType().getKind()!=Struct.Int||t3.getType().getKind()!=Struct.Int){
		 	parser.report_error("Greska na liniji: "+t1left+". Tip promenljive u izrazu mora biti Int.",null);
		 	RESULT=MyTab.noObj;
		 }
		 Code.put(Code.dup_x2);
		 Code.put(Code.pop);
		 Code.put(Code.dup_x2);
		 Code.put(Code.pop);
		 Code.put(Code.dup_x2);
		 Code.put(Code.add);
		 Code.put(Code.dup_x2);
		 Code.put(Code.pop);
		 Code.put(Code.sub);
		 Code.put(Code.mul);
		 RESULT = t1;
		 :}
		; 
Designator ::= Designator:d DOT IDENT:i
			   {:	
			  		if(d.getType().getKind() != Struct.Class) {
			   			parser.report_error("Semanticka greska na liniji " + dleft + ": ime " + d.getName() + " nije klasa!", null);
			   			RESULT = MyTab.noObj;
			   		}
			   		else {
			   			Obj ident = d.getType().getMembersTable().searchKey(i);
			   			if(ident == null) {
			   				parser.report_error("Semanticka greska na liniji " + ileft + ": ime " + i + " ne pripada klasi " + d.getName() + " !", null);
			   				RESULT = MyTab.noObj;
			   			}
			   			else {
			   				if(ident.getKind() == Obj.Fld) {
			   					parser.report_info("Koriscenje polja " + i + " na liniji " + ileft, null);
			   				}
			   				else if(ident.getKind() == Obj.Meth) { 
			   					parser.report_info(ident.getLevel()+"Koriscenje metode " + i + " na liniji " + ileft, null);
			   					
			   				}
			   				RESULT = ident;
			   			}
			   		}
			   :}
					|
	   		   IDENT:name 
	   		   {:   
	   		   		Obj simbol = MyTab.find(name);
			   		
			   		if(simbol == MyTab.noObj) 
			   			parser.report_error("Semanticka greska na liniji: " + nameleft + ", simbol " + name + " ne postoji u tabeli simbola!", null);
			   			
			   		else if(simbol.getKind() == Obj.Con) {
			   			parser.report_info("Koriscenje konstante " + name + " na liniji: " + nameleft, null);
			   		}
			   		else if(simbol.getKind() == Obj.Var||(simbol.getKind() == Obj.Type && simbol.getType().getKind() == Struct.Class)) {
			   			if(functionScope==MyTab.currentScope()) {
			   					if( functionScope.findSymbol(name) != null ) {
			   						parser.report_info("Koriscenje lokalne promenljive " + name + " na liniji " + nameleft, null);
			   					}
			   					else {
			   						parser.report_info("Koriscenje globalne promenljive " + name + " na liniji " + nameleft, null);
			   					}
			   				}
			   		}
			   		else if(simbol.getKind() == Obj.Meth) {
			   			if(MyTab.currentScope() == functionScope) {
			   				parser.report_info("Koriscenje globalne funkcije " + name + " na liniji " + nameleft, null);
						   	/*if(func.getName().equalsIgnoreCase("chr")){
								if(a.equals(Struct.Int)){
									parser.report_error("Greska na liniji: "+funcleft+". Funkcija chr mora da ima Int kao parametar.",null);
									RESULT=MyTab.noType;
								}
							}
							if(func.getName().equalsIgnoreCase("ord")){
								if(a.equals(Struct.Char)){
									parser.report_error("Greska na liniji: "+funcleft+". Funkcija ord mora da ima Char kao parametar.",null);
									RESULT=MyTab.noType;
								}
							}
							if(func.getName().equalsIgnoreCase("len")){
								if(a.equals(Struct.Array)){
									parser.report_error("Greska na liniji: "+funcleft+". Funkcija len mora da ima niz kao parametar.",null);
									RESULT=MyTab.noType;
								}
							
			   				}*/
			   				
			   			}
			   		}
			   		RESULT = simbol;
	   		   :}
	   		   |
			   Designator:o 
			   LMEDBRACE
			   {:
			   	   	Obj ime=MyTab.find(o.getName());
			   	   	if(ime==MyTab.noObj)
			   	   	{
			   	   		parser.report_error("Greska na liniji "  + oleft + ":niz nije deklarisan.",null);
			   	   		RESULT=MyTab.noObj;
			   	   	}
			   	    if (o.getType().getKind()==Struct.Array) { 
				   		RESULT = new Obj(Obj.Elem, "", o.getType().getElemType());
				   		Code.load(o);
				   		if(MyTab.currentScope() == functionScope) {
			   					if( functionScope.findSymbol(o.getName()) != null ) {
			   						parser.report_info("Koriscenje lokalnog niza " + o.getName() + " na liniji " + oleft, null);
			   					}
			   					else {
			   						parser.report_info("Koriscenje globalnog niza " + o.getName() + " na liniji " + oleft, null);
			   					}
			   				}
			   		} else {
			   			parser.report_error("Greska na liniji "  + oleft + ":tip nije niz",null);
			   			RESULT=MyTab.noObj;
			   		}
			   :} 
			   Expr:e
			   {:
			   	if(e.getType().getKind()!=Struct.Int) {
				  		parser.report_error("Semanticka greska na liniji " + eleft + " izraz u uglastim zagradama mora biti tipa int!", null);
				  		RESULT=MyTab.noObj;
				  	}
			   :}
			   RMEDBRACE
			   {:
			   	RESULT = new Obj(Obj.Elem, "elem", o.getType().getElemType());
			   :}
	   		   ;
Factor ::=NUMBER:n 
		 {:  
		 	 Obj c=new Obj(Obj.Con,"",MyTab.intType);
		 	 c.setAdr(n.intValue());
		 	 Code.load(c);
			 RESULT=c;
		 :}
		 |
		 CHAR:ch
		 {: 
		 	Obj c = new Obj(Obj.Con, "", MyTab.charType);
		   	c.setAdr(ch);
		   	Code.load(c);
		 	RESULT=c;
		 :}
		 |
		 BOOL:b
		 {:	
		 	Obj c = new Obj(Obj.Con, "", MyTab.boolType);
		   	if(b==true)
		   		c.setAdr(1);
		   	else 
		   		c.setAdr(0);
		   	Code.load(c);
		 	RESULT=c;
		 :}
		 |
		 Designator:func {:
		 	callingStack.push(func);
			actParsStack.push(0);
		 :}
		 LPAREN ActualPars:a RPAREN
		{:
			
			if(Obj.Meth == func.getKind()) {
				parser.report_info("Pronadjen poziv funkcije "+func.getName()+" na liniji: "+funcleft,null);
				int formalPars = func.getLevel();
				if(formalPars > actParsStack.peek()) {
					parser.report_error("Semanticka greska na liniji " + funcleft + ": broj stvarnih i formalnih parametara mora biti isti!", null);
					RESULT=MyTab.noObj;
				}
				else {
					int destAdr = func.getAdr() - Code.pc;
					Code.put(Code.call);
					Code.put2(destAdr);
				}
			}
			else {
				parser.report_error("Greska na liniji: "+funcleft+" ime: "+func.getName()+" nije funkcija.",null);
				RESULT=MyTab.noObj;
			}
			actParsStack.pop();
			callingStack.pop();
			RESULT = func;
		:}
		 |
		 NEW Type:t LMEDBRACE Expr:e RMEDBRACE
		 {:
		 	Struct arrayType = new Struct(Struct.Array);
			arrayType.setElementType(t);
			Obj obj=new Obj(Obj.Var, "t", arrayType);
		 	RESULT =obj;
		 	if(e.getType().getKind()!=Struct.Int){
		 	parser.report_error("Greska na liniji: "+eleft+". Tip promenljive u nizu mora biti Int.",null);
		 	RESULT=MyTab.noObj;
		 	}
		 	else{
		 			Code.put(Code.newarray);
				   	
				   	if(t.getKind() == Struct.Char)
				   		Code.put(0);
				   	else
				   		Code.put(1);
		 	}
		 :}
		 |
		  NEW Type:t
		  {:
		  	Obj obj=new Obj(Obj.Var, "t", t);
		  	RESULT = obj;
		  	if(t.getKind() != Struct.Class) {
			   		parser.report_error("Semanticka greska na liniji " + tleft + ": Tip mora biti klasa!", null);
			   		RESULT = MyTab.noObj;
			}
		  :}
		 |
		 LPAREN Expr:t RPAREN
		 {:
		 	RESULT=t;
		 :}
		 |
		 Designator:d
		 {:
		 	Code.load(d);
		 	RESULT=d;
		 :}
		 ;

ActualPars ::= ActPars | /*epsilon*/;
ActPars ::= ActPars COMMA Expr:e
		    {: 
		    	int ap = actParsStack.pop();
				ap++;
				actParsStack.push(ap);
				if(!callingStack.isEmpty() && callingStack.peek().getKind() == Obj.Meth) {
					if(actParsStack.peek() > callingStack.peek().getLevel()) {
						parser.report_error("Semanticka greska na liniji " + eleft + ": broj formalnih i stvarnih parametara funkcije mora biti isti!", null);
					}	
				}		
		    :}
			 |
		    Expr:e 
		    {: 
		    	int ap = actParsStack.pop();
				ap++;
				actParsStack.push(ap);
				if(!callingStack.isEmpty() && callingStack.peek().getKind() == Obj.Meth) {
					if(actParsStack.peek() > callingStack.peek().getLevel()) {
						parser.report_error("Semanticka greska na liniji " + eleft + ": broj formalnih i stvarnih parametara funkcije mora biti isti!", null);
					}	
				}		
		    :};
Assignop ::= EQUAL {:RESULT=new Integer(Code.eq); :}
			| 
			AddopRight:ar{:RESULT=(Integer)ar; :}
			| 
			MulopRight:mr{:RESULT=(Integer)mr; :}; 
Relop ::= EQEQ
		{:RESULT=Code.eq; :} 
		| 
		NOTEQ
		{:RESULT=Code.ne; :} 
		| 
		GREATER
		{:RESULT=Code.gt; :} 
		| 
		GREATEQ 
		{:RESULT=Code.ge; :}
		|
		LOWER 
		{:RESULT=Code.lt; :}
		| 
		LOWEQ
		{:RESULT=Code.le; :};
Addop ::= AddopLeft:al {: RESULT=(Integer)al; :}
		 | 
		 AddopRight:ar {: RESULT=(Integer)ar; :};
AddopLeft ::= PLUS 
			{:RESULT=Code.add; :}
			| 
			MINUS
			{:RESULT=Code.sub; :} ;
AddopRight ::= PLUSEQ
			{:RESULT=Code.add; :} 
			 | 
			 MINUSEQ
			 {:RESULT=Code.sub; :};
Mulop ::= 
		MulopLeft:ml {: RESULT=(Integer)ml; :}
		| 
		MulopRight:mr {: RESULT=(Integer)mr; :}
		;
MulopLeft ::= MULI
			{:RESULT=Code.mul; left=true; :} 
			|
			DIVI
			{:RESULT=Code.div; left=true; :} 
			| 
			MODI
			{:RESULT=Code.rem; left=true; :}
			|
			DOLAR
			{:RESULT=1; left=true; :};
			
MulopRight ::= MULEQ
		      {:RESULT=Code.mul; :} 
			  | 
			  DIVEQ
			  {:RESULT=Code.div; :}  
			  |
			  MODEQ
			  {:RESULT=Code.rem; :} ;